apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion
    defaultConfig {
        applicationId "com.oliver.weatherapp"
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        versionCode VERSION_CODE
        versionName VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file('weatherapp.jks')
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        all {
            signingConfig signingConfigs.release

            buildConfigField "String", "BASE_URL", '"http://api.openweathermap.org"'
            buildConfigField "String", "SERVER_URL", '"http://api.openweathermap.org/data/2.5/forecast"'
            buildConfigField "String", "API_KEY", '"c7b7f8988712ed045145446d06b3931b"'
        }
        debug {
            shrinkResources = false
            useProguard false   // don't obfuscate

            splits.abi.enable = false
            splits.density.enable = false
            aaptOptions.cruncherEnabled = false

            // Only use this flag on builds you don't proguard or upload to beta-by-crashlytics
            ext.alwaysUpdateBuildId = false
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
        }
        release {
            minifyEnabled true  // shrink
            useProguard true    // obfuscate
            shrinkResources = true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "WeatherApp-${variant.buildType.name}-${variant.versionName}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


    dataBinding.enabled = true
    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation "com.android.support:support-v4:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation "com.android.support:design:${support_version}"
    implementation "com.android.support:recyclerview-v7:${support_version}"

    implementation 'com.google.android.gms:play-services-places:15.0.1'

    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.lifecycle:reactivestreams:$arch_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    kapt "android.arch.persistence.room:compiler:$room_version"

    implementation "android.arch.lifecycle:runtime:$arch_version"
    implementation "android.arch.lifecycle:extensions:$arch_version"
    kapt "android.arch.lifecycle:compiler:$arch_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.jakewharton.timber:timber:4.7.0'

    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    implementation 'com.google.firebase:firebase-core:15.0.2'
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true
    }

    implementation "io.reactivex.rxjava2:rxjava:2.1.6"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"

    // Retrofit
    implementation "com.google.code.gson:gson:${gsonVersion}"
    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"

    ///////////////////// TESTS DEPENDENCIES

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test:rules:1.0.2"
    androidTestImplementation "android.arch.core:core-testing:$arch_version"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:${espressoVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })


    testImplementation "junit:junit:${junitVersion}"
    testImplementation "com.nhaarman:mockito-kotlin:${mockitoVersion}"
}

apply plugin: 'com.google.gms.google-services'
